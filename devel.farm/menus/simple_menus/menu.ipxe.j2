#!ipxe
########## DEFAULTS ##########
# Menu defaults
set menu-timeout 60000
set submenu-timeout ${menu-timeout}
isset ${menu-default} || set menu-default exit

# Figure out if client is 64-bit capable
cpuid --ext 29 && set arch x64 || set arch x86
cpuid --ext 29 && set archl amd64 || set archl i386

########## MAIN MENU ##########
:start
menu iPXE boot menu
item --gap --              ------------------------- Installs ---------------------------------------
item fedora32              Boot fedora32 from WEB
item fedora33aarch64       Boot fedora 33 aarch64 from WEB
item ubuntufocal           Boot ubuntu focal from WEB
item --gap --              ------------------------- Tools ------------------------------------------
item menu-recovery         Recovery tools... (no-op)
item menu-diag             Diagnostics tools... (no-op)
item --gap --              ------------------------- Advanced ---------------------------------------
item --key c config        Configure settings
item --key s shell         Drop to iPXE shell
item --key r reboot        Reboot computer
item --gap --              ------------------------- Default ----------------------------------------
item --key x exit          Exit iPXE and continue normal boot

choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
set menu-timeout 0
goto ${selected}

:cancel
echo You cancelled the menu, dropping you to a shell

########## ITEMS ##########
##### Installs
:fedora32
dhcp
kernel ${IPXE_FEDORA_MIRROR}32/Server/x86_64/os/images/pxeboot/vmlinuz inst.repo=${IPXE_FEDORA_MIRROR}32/Server/x86_64/os/ ip=dhcp initrd=initrd.img
initrd ${IPXE_FEDORA_MIRROR}32/Server/x86_64/os/images/pxeboot/initrd.img
boot          

:fedora33aarch64
dhcp
kernel ${IPXE_FEDORA_MIRROR}33/Everything/aarch64/os/images/pxeboot/vmlinuz inst.repo=${IPXE_FEDORA_MIRROR}33/Everything/aarch64/os/ ip=dhcp initrd=initrd.img
initrd ${IPXE_FEDORA_MIRROR}33/Everything/aarch64/os/images/pxeboot/initrd.img
boot  

:ubuntufocal
dhcp
set base-url http://archive.ubuntu.com/ubuntu/dists/focal/main/installer-amd64/current/legacy-images/netboot/ubuntu-installer/amd64/
kernel ${base-url}/linux console=ttyS1,115200n8
initrd ${base-url}/initrd.gz
boot

##### Tools

##### Advanced
:config
config
goto start

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:reboot
reboot

##### Default
:exit
exit
